buildscript {
	ext {
		springBootVersion = "1.5.7.RELEASE"
		asciiDoctorPluginVersion = "1.5.3"
	}
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.asciidoctor:asciidoctor-gradle-plugin:${asciiDoctorPluginVersion}")
	}
}

apply plugin: "checkstyle"
apply plugin: "java"
apply plugin: "idea"
apply plugin: "org.springframework.boot"
apply plugin: "org.asciidoctor.convert"

group = "com.farmers.css"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	compile("org.springframework.boot:spring-boot-starter-actuator")
	compile("org.springframework.boot:spring-boot-starter-web")
	compile("org.flywaydb:flyway-core:4.2.0")


	testCompile("org.springframework.boot:spring-boot-starter-test")
	testCompile("org.springframework.restdocs:spring-restdocs-core:1.2.1.RELEASE")
	testCompile("org.springframework.restdocs:spring-restdocs-mockmvc:1.2.1.RELEASE")
}

checkstyle {
	toolVersion = "8.2"
	ignoreFailures = false
	configFile = rootProject.file("config/checkstyle.xml")
}

ext {
	snippetsDir = file("build/generated-snippets")
}

asciidoctor {
	attributes "snippets": snippetsDir
	inputs.dir snippetsDir
	outputDir "${project.buildDir}/resources/static"
	dependsOn test
	sourceDir "src/main/resources/asciidoc"
	sources {
		include "index.adoc"
	}
}

jar {
	baseName = "css-hello-world"
	version =  "0.0.1"

	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into "static"
	}
}

test {
	outputs.dir snippetsDir
}

test.finalizedBy asciidoctor
